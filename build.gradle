buildscript {
    repositories {
        jcenter()
        maven {
            name "forge"
            url "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'java'
    id 'idea'
    id "com.wynprice.cursemaven" version "2.1.4"
}

idea {
    module {
        // For some reason this is necessary for IDEA 2014 workspaces to recognize assets
        inheritOutputDirs = true
        //Exclude directories from being managed
        for (String excludeDirName in ["run", "out", "logs", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}

apply plugin: 'net.minecraftforge.gradle'

group 'gg.galaxygaming.gasconduits'
version = "${minecraft_version}-${mod_version}"
archivesBaseName = "${mod_name}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings channel: "stable", version: "${mappings_version}"

    runs {
        client {
            workingDirectory project.file("run")

            //The below if statements are to add args to your gradle.properties file in user home
            // (DO NOT add them directly to the gradle.properties file for this project)
            // Setting the below properties allows use of your normal Minecraft account in the
            // dev environment including having your skin load. Each property also has a comment
            // explaining what information to set the value to/format it expects
            // One thing to note is because of the caching that goes on, after changing these
            // variables, you need to refresh the project and rerun genIntellijRuns/genEclipseRuns
            if (project.hasProperty('mc_uuid')) {
                //Your uuid without any dashes in the middle
                args '--uuid', project.getProperty('mc_uuid')
            }
            if (project.hasProperty('mc_username')) {
                //Your username/display name, this is the name that shows up in chat
                // Note: This is not your email, even if you have a Mojang account
                args '--username', project.getProperty('mc_username')
            }
            if (project.hasProperty('mc_accessToken')) {
                //Your access token, you can find it in your '.minecraft/launcher_profiles.json' file
                args '--accessToken', project.getProperty('mc_accessToken')
            }

            mods {
                gasconduits {
                    source(sourceSets.main)
                }
            }
        }

        server {
            workingDirectory project.file("run")

            mods {
                gasconduits {
                    source(sourceSets.main)
                }
            }
        }
    }
}

repositories {
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name = "EnderIO"
        url = "https://maven.tterrag.com/"
    }
    maven {
        name = "JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei_${minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei_${minecraft_version}:${jei_version}")
    compile fg.deobf("curse.maven:mekanism:${mekanism_id}")

    compile "info.loenwind.autoconfig:AutoConfig:${minecraft_version}-${autoconfig_version}"
    compile "info.loenwind.autosave:AutoSave:${minecraft_version}-${autosave_version}"
    compile fg.deobf("com.enderio.core:EnderCore:${minecraft_version}-${endercore_version}")
    compile fg.deobf(project.dependencies.create("com.enderio:EnderIO:${minecraft_version}-${enderio_version}") {
        transitive = false
    })
}

task replaceResources(type: Copy) {
    outputs.upToDateWhen { false }
    from(sourceSets.main.resources) {
        include "mcmod.info"
        expand "version": mod_version, "mc_version": minecraft_version
    }
    into "$buildDir/resources/main/"
}

processResources {
    //Exclude the mcmod.info file as we manually handle that and don't want it to invalidate our cache
    exclude 'mcmod.info'
    finalizedBy(replaceResources)
}